(defun my/todo ()
  (interactive)
  (let* ((val (read-string "Priority (default 1000): "))
         (prio-to-insert (if (string= val "") "1000" val)))
    (insert "\n")
    (insert "//=====================\n")
    (insert "// TODO :")
    (let ((todo-pos (point)))
      (insert "\n")
      (insert (format "// Prio : %s" prio-to-insert))
      (insert "\n")
      (insert "//=====================\n")
      (goto-char todo-pos))))

(defun my/grep-todos-jump ()
  "Jump to the todo on the current line."
  (interactive)
  (let ((data (get-text-property (point) 'todo-data)))
    (if data
        (let ((file (car data))
              (line (cdr data)))
          (find-file file)
          (goto-line line))
      (message "No todo found on this line."))))

(defun my/grep-todos ()
  (interactive)
  (let* ((dir (read-directory-name "Search directory: "))
         (search-dir (expand-file-name dir))
         (default-directory search-dir)
         (output (shell-command-to-string "rg -n --no-heading -B 1 \"// Prio :\" ."))
         (todos '()))
    (let ((lines (split-string output "\n")))
      (dotimes (i (length lines))
        (let ((current-line (nth i lines)))
          (when (string-match ":\\([0-9]+\\):.*// Prio : *\\([0-9]+\\)" current-line)
            (let ((prio (string-to-number (match-string 2 current-line)))
                  (prev-line (if (> i 0) (nth (1- i) lines) nil)))
              (when (and prev-line (string-match "^\\(.*\\)-\\([0-9]+\\)-.*// TODO : *\\(.*\\)" prev-line))
                (let ((file (match-string 1 prev-line))
                      (line (string-to-number (match-string 2 prev-line)))
                      (msg (match-string 3 prev-line)))
                  (push (list prio msg (expand-file-name file search-dir) line) todos))))))))
    (setq todos (sort todos (lambda (a b) (< (car a) (car b)))))
    (let ((buf (get-buffer-create "*Todo List*")))
      (switch-to-buffer buf)
      (let ((inhibit-read-only t))
        (erase-buffer)
        (insert (format "%-8s | %-40s | %s\n" "Priority" "Task" "File"))
        (insert (make-string 80 ?-))
        (insert "\n")
        (dolist (item todos)
          (let ((prio (nth 0 item))
                (msg (nth 1 item))
                (file (nth 2 item))
                (line (nth 3 item)))
            (insert (propertize (format "%-8d | %-40s | %s\n" 
                                        prio 
                                        msg 
                                        (file-relative-name file search-dir))
                                'todo-data (cons file line)
                                'help-echo (format "File: %s\nLine: %d" file line))))))
      (use-local-map (let ((map (make-sparse-keymap)))
                       (define-key map (kbd "RET") 'my/grep-todos-jump)
                       map))
      (setq buffer-read-only t))))
